// depends on android and android-command plugins
// apply this inside the android closure
// like: apply from: 'monkey.gradle', to: delegate

def runMonkeyAllTask = project.task('runMonkeyAll')

android.command.devices().eachWithIndex { device, index ->

    def showOverlayTask = project.task("showOverlayDevice${index}", type: com.novoda.monkey.NotificationBarOverlay) {
        show = true
        deviceId = device.id
    }

    def monkeyTask = project.task("runMonkeyDevice${index}", type: com.novoda.monkey.TargetedMonkey, dependsOn: 'installDebug') {
        packageName = "com.novoda.monkey"
        events = 10000
        deviceId = device.id
        logFileName = 'monkey.log'
        categories = ["android.intent.category.MONKEY"]
    }

    def hideOverlay = project.task("hideOverlayDevice${index}", type: com.novoda.monkey.NotificationBarOverlay) {
        show = false
        deviceId = device.id
    }

    monkeyTask.dependsOn showOverlayTask
    monkeyTask.finalizedBy hideOverlay
    runMonkeyAllTask.dependsOn monkeyTask
}
